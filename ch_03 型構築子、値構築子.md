# Chapter 3. 型構築子、値構築子

## 型構築子（型コンストラクタ）
Haskell では `data` というキーワードを用いて新しい「型」を作ることができます。<br>
```Haskell
-- 本の ID、書籍名、著者名（複数人）の情報を持つデータをイメージしています。
data BookInfo = Book Int String [String]
```

`data` に続く `BookInfo` は「型」の名前を表し、**型構築子（型コンストラクタ）** と呼ばれます。<br>

その後の `Book` は **値構築子（値コンストラクタ）** と呼ばれ、`Book` という識別子で `BookInfo` 型として利用できるデータを作る、という意味になります。<br>
`Book` に続く `Int String [String]` は、`Book` が持つデータは `Int` と `String` と `[String]` の３つとなる、という意味になります。<br>

以下のようにすると、具体的な値を作ることができます。
```Haskell
book1 = Book 1 "Kids Science" ["Mike", "Bob"]  -- イメージとしては、本の ID、書籍名、複数の著者名
```
これで、`book1` は `BookInfo` 型の値で、上記の３つのデータを持つものとして利用できるようになります。

## 値構築子（値コンストラクタ）
Haskell は、以下のように「１つの型」で複数のデータ構造を持つことができます。<br>
`|` は、一般的な言語と同様に「or」の意味を表すものと考えてください。

```Haskell
data BookInfo = Book Int String [String]
              | BookPrice Int String Int  -- 本の ID、書籍名、価格をイメージしています。
```

上のようにすると、`BookInfo` という型は２種類のデータ構造を持つものとなり、
以下のように、２種類の方法で `BookInfo` の値を作ることができるようになります。
```
book1 = Book 1 "Kids Science" ["Mike", "Bob"]
book2 = BookPrice 2 "Kids Math" 500
```

`BookInfo` 型に `BookPrice` のような値構築子を追加した場合、その型の値は、著者名のデータを持たない場合もある、
というような注意が必要となります。<br>

Haskell では、このような複数の値構築子を持つことの方が普通であり、さらに、複数ある値構築子は
条件分岐の道具として利用されることが多い、ということを念頭においておくと良いと思います。<br>
Haskell では、条件分岐をさせるために複数の値構築子を準備する、
というスタイルをとることがよくあります。

> Tips<br>
> Haskell の「型」は、一般的なプログラミング言語では、discriminated union とイメージが一致します。

## レコード構文
先程は、初歩の例として値構築子の使い方を説明しましたが、
値構築子は以下のような **レコード構文** を用いてを記述することの方が一般的です。

```Haskell
data BookInfo = Book {
   bookID :: Int,  -- フィールド名 :: 型 の順序で記述します。
   bookTitle :: String,
   bookAuthors :: [String]
}
```

レコード構文を用いると、次のように値を生成することができます。
```Haskell
-- レコード構文を用いて書くと、それぞれの値の意味が分かりやすい。
myBook1 = Book { bookID = 1, bookTitle = "Science", bookAuthors = ["John"] }

-- もちろん、レコード構文を用いずに書くこともできます。
myBook2 = Book 2 "Pure Code" ["Mike", "Bob"]
```

レコード構文に付けた各フィールド名は、**値を取り出す「関数」** としても利用できるようになります。<br>
上の方で定義した `bookID` や、`bookTitle` は **関数** として利用できます。

```Haskell
ghci> bookTitle myBook1
"Science"

ghci> bookID myBook1
1
```

`bookID` は関数であるので、その型をとることができます。
```
ghci> :t bookTitle
bookTitle :: BookInfo -> String  -- BookInfo から、文字列を評価値として生み出す関数と認識されています。
```

## 値構築子で条件分岐をさせる例
以下のような平面上の点を表す型を考えてみます。<br>
`Polar` は極座標を表すものと考えてください。（`r` が原点からの距離、`t` が偏角を表しています。）

```Haskell
data Point2D
    = XY { x :: Double, y :: Double }
    | Polar { r :: Double, t :: Double }
    deriving Show
```
`deriving Show` といのは、`Point2D` の型を表示できるようにするものです。今は特に気にしないでください。

極座標を与えられた場合、それを `XY` 形式に変換する関数は以下のように書けます。
```Haskell
transXY (Polar r t) = XY (r * cos t) (r * sin t)
```

`=` が書き換えのルールを表している、という感覚が伝わると思います。<br>
C言語のような代入の意味の `=` とは感覚が異なります。

上の２つを ghci に読み込ませると、以下のように操作ができるようになります。
```
ghci> p = Polar 1 (pi/4)

ghci> transXY p
XY {x = 0.7071067811865476, y = 0.7071067811865475} 

-- transXY の関数の型も正しく認識されていることを確認してみましょう。
ghci> :t transXY
transXY :: Point2D -> Point2D
```

`transXY` の型は `Point2D -> Point2D` ですが、値構築子 `XY` で作った `Point2D` の値を適用するとエラーになります。<br>
`XY x y` 形式の値を受け取ったときについて、何も指定されていないからですね。
```
ghci> q = XY 1 1
ghci> transXY q
エラーが表示されます。
```

次に、`transXY` を利用して、２点間の距離を求める関数 `dist` を作ってみましょう。<br>
以下のような Haskell のパターンマッチの文法を利用することにします。
```Haskell
case A of
    B -> X
    C -> Y
```
この式の意味は、`A` が  `B` に当てはまる場合は `X` を評価し、`C` に当てはまる場合は `Y` を評価する、というものになります。

**リスト１**
```Haskell
dist pt1 pt2
    = case pt1 of
        XY x1 y1 -> case pt2 of
            XY x2 y2 -> sqrt ((x1 - x2)^2 + (y1 - y2)^2)
            Polar _ _ -> dist pt1 (transXY pt2)
        Polar _ _ -> dist (transXY pt1) pt2
```
リスト１で、`Polar _ _` となっているのは、`Polar r t` と書いてもいいのですが、
ここでは `Polar` という形に当てはまればいいだけで、`r` や `t` の値はなんでもいいですよ、ということを表しています。

---
**リスト１** を実行したい場合、以下のようにしてください。<br>

* step１
適当な名前のファイルを作り（ここでは、`Test.hs` としておきます）以下の内容を保存してください。<br>
```
data Point2D
    = XY { x :: Double, y :: Double }
    | Polar { r :: Double, t :: Double }
    deriving Show

transXY (Polar r t) = XY (r * cos t) (r * sin t)

dist pt1 pt2
    = case pt1 of
        XY x1 y1 -> case pt2 of
            XY x2 y2 -> sqrt ((x1 - x2)^2 + (y1 - y2)^2)
            Polar _ _ -> dist pt1 (transXY pt2)
        Polar _ _ -> dist (transXY pt1) pt2
```

> ファイルの保存の操作に慣れていない人へ<br>
> まず、「Ctrl + D」を押して、ghci を抜けてください。<br>
> `$` や `#` のようなプロンプトが画面に表示されたら `vim Test.hs` と入力し、
> 上記の内容をコピーペーストして保存します。<br>
>
> `vim` の操作が分からない場合や、`vim` のインストールが必要な場合は、インターネットで検索するとすぐに情報が見つかります。<br>
> もし、Windows の WSL 上で実行している場合でしたら、`vim Test.hs` と入力した後、
> 上の内容をコピーし、`vim` の画面上で右クリックをすると内容がペーストされるので、「Esc」→「:」→「w」→「q」→「Enter」の順で
> キーを押してください。そうすると、`Test.hs` というファイルができあがります。

* step２
ghci を抜けてしまっている場合は、`ghci` と入力して ghci に戻ってください。<br>
`:load`（`:l`）コマンドで `Test.hs` をロードします。
```
ghci> :l Test.hs
```

* step３
関数 `dist` を実行してみてください。
```
ghci> p = XY 1 1
ghci> q = XY 2 2
ghci> r = Polar 2 (pi/3)

ghci> dist p q
1.4142135623730951

ghci> dist p r
0.7320508075688772
```

