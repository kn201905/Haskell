# Chapter 8. 型クラス

`class　型クラス名　型構築子　where` の形で **型クラス** が定義される。

`Eq` を例にとると、以下のようになる。

```Haskell
class Eq a where
   (==) :: a -> a -> Bool
   (/=) :: a -> a -> Bool
``` 

上の例でいうと、「型 `a` が `Eq` の性質を持つならば、２つの関数 `==` と `/=` の引数になることができる」という意味になる。<br>
ある型クラスに属する型は、その型クラスで定義されている関数を利用できる、というイメージである。

また、そのような場合、「型 `a` は `Eq` の **インスタンス** である」という。<br>
<br>

例として、２次元の座標を表す型 `Pt` を考えてみよう。

```Haskell
data Pt = Pt Double Double
```

このとき、「`Pt` に等号で比較できる性質を持たせたい」と思った場合、以下のように `Eq` のインスタンスにすればよい。

```Haskell
instance Eq Pt where
   (Pt x y) == (Pt x' y') = x == x' && y == y'
```
<br>

`Eq` には以下の定義が含まれているため、`==` を定義するだけで、自動的に `/=` も定義される。そのため、`Pt` には `==` の定義を書くだけでよい（`\=` の定義は書かなくてよい）、という仕組みになっている。

```Haskell
   x == y = not (x /= y)
   x /= y = not (x == y)
```

`Pt` の動作確認。

```Haskell
ghci> Pt 1 2 == Pt 1 2
True

ghci> Pt 1 2 == Pt 3 4
False
```
---
簡単に言うと以下のようになる。

**型クラス** とは、特定の性質をもった **関数の集まり** である。<br>
特定の性質をもった関数とは、例えば比較に関するもの（「==」や「>」）、数値計算に関するもの（「+」や「-」）などがある。

次に、型クラスＡがあったとする。Ａに含まれる関数すべてを利用できるようになった型を、型クラスＡの **インスタンス** である、という。

---
型クラスを作るために、関数の集まりを宣言するときに **class** を用いる。

ある型があって、その型を型クラスのインスタンスにするために、関数をどのように適用するかを定義するときに **instance** を用いる。

---

`Maybe` のような `型変数` を受け取るものの場合、以下のようにインスタンス化される。
```Haskell
instance (Eq a) => Eq (Maybe a) where
   Just x == Justy = x == y  -- ここで x == y という比較をするため、「(Eq a) =>」という制約が付いている
   Nothing == Nothing = True
   _ == _ = False
```

## 型クラスの例
実際によく見かける型クラスを見ると、どのような関数の集まりが定義されているか分かりやすいため、２つほど例をあげよう。

* 大小比較に関する型クラス
```
class Eq a => Ord a where
  compare :: a -> a -> Ordering
  (<) :: a -> a -> Bool
  (<=) :: a -> a -> Bool
  (>) :: a -> a -> Bool
  (>=) :: a -> a -> Bool
  max :: a -> a -> a
  min :: a -> a -> a
```
大小比較するのだから、`==` は当然必要ということで、`Eq a =>` という制約が付いている。

* 計算に関する型クラス
```
class Num a where
  (+) :: a -> a -> a
  (-) :: a -> a -> a
  (*) :: a -> a -> a
  negate :: a -> a
  abs :: a -> a
  signum :: a -> a
  fromInteger :: Integer -> a
```

## 型クラス Functor

`Functor` は少し分かりにくい型クラスであるが、Haskell において重要であるため、丁寧に見てみよう。<br>

以下の機能をもった型を `Functor` という。

```Haskell
class Functor f where
   fmap :: (a -> b) -> f a -> f b
```
上記において `f` の場所には、`型構築子` が入るため、`f a` という表記から、`f` は、型変数を１つだけ受け取る型である、というところまで読み取っておいてほしい。

`Functor` のインスタンスになるためには、`fmap` が適用できるような型になればよい、というだけなのだが、少し分かりにくいと思うため、いくつか例をあげよう。

---
`Functor` のインスタンスで最も分かりやすいのはリスト型 `[a] = [] a` だと思う。<br>
リスト型に対する `fmap` は `map` である。
```Haskell
instance Functor [] where
   fmap = map
```
```Haskell
ghci> fmap (show :: Int -> String) [1,2,3,10,11,12]
["1","2","3","10","11","12"]
```

上記の例では、`fmap` は `Int -> String` と `[] Int` を受け取り、`[] String` の値を返している。<br>
つまり、`[] Int` 型は、`Functor` のインスタンスとなっている。

---
`Maybe` も `Functor` となる。定義は以下のとおり。<br>
```Haskell
instance Functor Maybe where
   fmap f (Just x) = Just(f x)
   fmap f Nothing  = Nothing
```

```Haskell
ghci> fmap (show :: Int -> String) (Just 100)
Just "100"
```
確かに、`Int -> String` と `Maybe Int` を受け取り、`Maybe String` の値を返している。

以下に `Nothing` が返される例も挙げておく。
```Haskell
ghci> fmap ("abc"++) (Just "def")
Just "abcdef"

ghci> fmap ("abc"++) Nothing
Nothing
```

---
`Either` も `Functor` となるが、`Either` は型変数を「２つ」とるため、定義の仕方に注意が必要となる。<br>
以下のことをもう一度読み返すと、型変数が「２つ」あることがダメな理由が分かると思う。

> `f` の場所には、`型コンストラクタ` が入るため、`f a` という表記から、`f` は、型変数を１つだけ受け取る型である、というところまで読み取っておいてほしい。

`Either a b` を、「`Either a` が、型変数 `b` を受け取る」と考える。Haskell では、型コンストラクタも部分適用が可能になっている。

```Haskell
instance Functor (Either a) where
   fmap f (Left x) = Left x
   fmap f (Right x) = Right (f x)
```

動作を確認してみよう。
```Haskell
ghci> fmap show (Right 10)
Right "10"
```
`show :: Int -> String` と `Either String Int` を受け取って、`Either String String` を返している。

```Haskell
ghci> fmap show (Left "error")
Left "error"
```
確かに、`Int -> String` と `Either String Int` を受け取って、`Either String String` を返している。

---
ここまでの例を見て分かるように、何かを包むような型は、おおよそ `Functor` の性質を持つことができる（`fmap` を実装することができる）。

これにより、`Maybe` や `Either` などをエラー検知の道具として非常に便利に活用できるようになっている。<br>
C 言語や Python などであれば、エラーが発生したら if や例外などで途中分岐して実行の流れを変えていくようになると思う。しかし、Haskell は、引数を受け取り結果を返していくことを「連続的に」行うことにより実行を進めるため、`Maybe` などで結果を包んで後続の関数に結果を渡し、後続の関数では `fmap` を用いることにより、先行の関数がエラーを発生させていたとしても、何の問題もなく処理を進めていけるようにしているのである。

そのため、`Maybe` の `Functor` へのインスタンス化で見たように、`Nothing` を渡されたら、どんな関数 `f` が渡されようとも、`f` には関係なく `Nothing` を返す、という実装になる。

以下の例を見てみてほしい。
```Haskell
class StrToInt a where
   strToInt :: a -> Maybe Int
   
instance StrToInt String where
   strToInt x = Just $ (read x :: Int)

instance StrToInt Int where
   strToInt x = Nothing
```
```Haskell
ghci> x = "123"
ghci> strToInt x
Just 123

ghci> y = 100::Int
ghci> strToInt y
Nothing

ghci> fmap (2*) (strToInt x)
Just 246

ghci> fmap (2*) (strToInt y)
Nothing
```
上の例では、`x` の方は文字列から数値に正しく変換されて `2*` が適用されている。<br>
しかし、`y` の方は `Int` であるのに間違えて `strToInt` を適用してしまった、という状況を考えている。しかし、`Maybe` は `Fucntor` のインスタンスになっているため、`strToInt` の結果を `(2*)` に渡しても問題なく動作を続けることができるようになっている。

また、`fmap` を先頭に書くと可読性が落ちるため、中置記法として `<$>` が用意されている。関数 `F` を、`Functor` の値 `x` に、`fmap` を用いて適用したい場合、`fmap F x` を `F <$> x` と自然な形で書くことができる。<br>
中置記法での例。
```Haskell
ghci> (2*) <$> (strToInt x)
Just 246

ghci> (2*) <$> (strToInt y)
Nothing

ghci> (10+) <$> (2*) <$> (strToInt x)
Just 256

ghci> (10+) <$> (2*) <$> (strToInt y)
Nothing
```
上の例を見てもらうと、`fmap` を持つことにより、エラー処理がうまく進められることが分かると思う。<br>
これが、Haskell において、`Functor` が重要となる理由になる。

---
中置記法を見て気付けるように、通常の関数も `Functor` となる。

関数の型コンストラクタは `->` であり、`r -> a` は `(->) r a` となる。型変数を２つとるため、`Either` と同じように考えて、以下のように定義される。

```Haskell
instance Functor ((->) r) where
   fmap = (.)
```
上の場合、`fmap` のシグネチャを考えてみると `fmap :: (a -> b) ((->) r a) ((->) r b)` は、
`(a -> b) (r -> a) (r -> b)` となり、合成関数を表すことになる。<br>
関数に `fmap` を適用してみよう。
```Haskell
ghci> (fmap length show) 123
3
```
fmap は、`String -> Int` と `(->) Int String` を受け取り、`(->) Int Int` を返している。

