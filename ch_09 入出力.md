# Chapter 9. 入出力

以前に `IO` 型構築子について触れたが、ここでもう少し詳しく見ることにする。<br>
まず、`putStrLn` について見てみよう。
```
ghci> putStrLn "Hello"
Hello

ghci> :t putStrLn  -- :t は :type の略
putStrLn :: String -> IO ()
```

上の型表示において `IO` は、入力または出力を行うアクションを意味し、`IO a` の形で用いられる。<br>
例えば `IO String` であれば、それはアクションを実行した結果、`String` 型のデータを受け取る、という意味になる。<br>
`putStrLn` は、画面に文字列を表示するのみで、アクションを実行した結果、何も受け取るものがないため `IO ()` という型になる。

次に、アクションでデータを受け取るものの例を見てみよう。
```
-- getLine を実行すると入力待ち状態になるので、abcdef と入力してみよう。
ghci> x <- getLine
abcdef

ghci> x
"abcdef"

ghci> :t getLine
getLine :: IO String
```
上の例を見れば、`getLine` が `IO String` 型であることが分かると思う。<br>
上の例において、`<-` は、アクションの実行によって得られた値を取り出す、という意味である。

## アクションを順次実行する方法
Haskell では、`関数が引数を受け取る -> 関数が実行される -> 結果が返される -> その結果を引数として次の関数が実行される` がプログラムの実行の流れとなるため、アクションに対しては `>>=` という演算子が用意されている。<br>
`A >>= B` は、アクション `A` で得られた値を関数 `B` に引数として渡す、という動作を表す。

**`>>=` の例**
```
ghci> getLine >>= putStrLn . map toUpper
abcdef
ABCDEF

-- もし、toUpper についてエラーが出る場合は、以下のことを試してみてください。
ghci> import Data.Char(toUpper)
```
`toUpper :: Char -> Char` は、小文字のアルファベットを大文字に変換する関数である。

`>>=` を用いて、`"abc", "def", "hij"` を表示するプログラムは以下のようになる。
```Haskell
main = putStrLn "abc"
      >>= \_ -> putStrLn "def"
      >>= \_ -> putStrLn "hij"
```

```
ghci> main
abc
def
hij
```
ここで、`putStrLn "abc" >>= putStrLn "def"` のように書くとエラーになることを確認しておいてほしい。<br>
`putStrLn :: String -> IO ()` であるので、`putStrLn "abc" >>=` で取り出される値は `()`。
これを `putStrLn` に渡すことはできないためエラーとなる。

そのため、`\_ -> putStrLn "def"` とラムダ式を作っておいて、それに `()` を渡すようにしている。
全くムダな式を書くことになるが、言語の仕様上仕方ない。

そこで、`>>= \_ ->` を自動的に補ってくれるのが `do` である。上の例とまったく同じ動作をするのが以下のものとなる。
```Haskell
main = do
   putStrLn "abc"
   putStrLn "def"
   putStrLn "hij"
```

## アクションの利用例
`IORef` を用いて、値を保持できるアクションを作ることができる。Haskell の値は不変であるが、アクションによって外部に値が保持されているようにすることで、一般的なプログラム言語の変数と同等のものを作ることができる。
```
IORef は IO モナドの中でしか使用することができない。似たようなものとして、ST モナドの中だけで使える STRef などもある。
```

| 関数名 | シグネチャ | 機能 |
| --- | --- | --- |
| `newIORef` | `a -> IO (IORef a)` | 新しく `IORef a` 型の値を生成する |
| `readIORef` | `IORef a -> IO a` | 値 `IO a` を取り出す |
| `writeIORef` | `IORef a -> a -> IO ()` | 値 `a` を書き込む |
| `modifyIORef` | `IORef a -> (a -> a) -> IO ()` | 値 `a` を変更する |


```Haskell
import Data.IORef

main = do
   a <- newIORef "abc"
   b <- readIORef a
   writeIORef a "xyz"
   putStrLn =<< readIORef a
   putStrLn b
   
   c <- newIORef 1
   putStrLn . show =<< readIORef c
   modifyIORef c (5+)
   putStrLn . show =<< readIORef c
```
```
ghci> main
xyz
abc
1
6
```

## return
以下の例を見てほしい。文字を入力する限り実行がされ続け、何も入力しなければ実行が終了する簡単なもの。
```Haskell
main = do
   x <- getLine
   if null x
      then return ()
      else do
         putStrLn x
         main
```
`return` は、一般的なプログラム言語と意味が異なるため注意が必要である。<br>
`return` の型は `return :: Monad m => a -> m a` である。未知の `Monad` という単語があるが、今は `IO` と読み替えてもらって差し支えない。<br>
今のところ、`return` は `return :: a -> IO a` であり、`return` は単純に値を `IO` でくるんだだけのものを返すもの、と考えてほしい。

上のプログラムが終了するのは、単に `if` から抜けて実行が終了したということである。`if` は値 `IO ()` を返し、それが `main` の戻り値となって、プログラムが終了している。

`return` は、単に `IO a` の値を作るだけなので、下のプログラムを実行させると、`"abc"` も `1` も表示される。
```Haskell
main = do
   x <- return "abc"
   putStrLn x
   y <- return 1
   putStrLn $ show y
```
`return "abc"` により、`IO String` の値が生成され、`<-` により、格納していた `String` の値が `x` に取り出された、と読めると良い。

> Tips<br>
> `putStrLn $ show` は `print` と書けば良い。`print` は、`Show` のインスタンスである値を受け取り、それを `Show` に通してから `putStrLn` を実行する関数である。
> ```
> ghci> :t print
> print :: Show a => a -> IO ()
>
> ghci> :t putStrLn
> putStrLn :: String -> IO ()
> ```

## ファイルからの入力 `openFile`

ファイル入出力は簡単で、以下の例を見てもらえば分かると思う。
```Haskell
import System.IO  

main = do
   hfile <- openFile "main.hs" ReadMode
   txts <- hGetContents hfile
   putStrLn txts
   hClose hfile
```

* `openFile :: FilePath -> IOMode -> IO Handle`
```
ghci> :i FilePath  -- :i は :info と同じ
type FilePath :: *
type FilePath = String

ghci> :i IOMode
type IOMode :: *
data IOMode = ReadMode | WriteMode | AppendMode | ReadWriteMode
```

* `hGetContents :: Handle -> IO String`
* `hClose :: Handle -> IO ()`

## ファイルからの入力 `withFile`

`openFile` を利用した場合は、自分で `hClose` を呼んでファイルのクローズを行う必要があった。<br>
ファイルのクローズ忘れはよくあるバグであるため、`withFile` という関数も用意されている。<br>
シグネチャは下のとおり。

`withFile :: FilePath -> IOMode -> (Handle -> IO r) -> IO r`

上のシグネチャにおいて、第３引数にファイルを処理したい関数を渡せば良い。以下がその例である。
```Haskell
main = do
   withFile "main.hs" ReadMode (\hfile -> do
      txts <- hGetContents hfile
      putStrLn txts)
```

`withFile` は、第３引数に渡した関数の実行を終了するとファイルを自動的にクローズする。<br>
従って、以下のようなプログラムはコンパイルはできるが正しく実行できない。
```Haskell
import System.IO

main = do
   txts <- withFile "main.hs" ReadMode hGetContents
   putStrLn txts
```

上のプログラムは、コンパイルもできるし、一見正しく実行できそうであるが、実際に実行すると以下のエラーが出力されて止まってしまう。
```
*** Exception: main.hs: hGetContents: illegal operation (delayed read on closed handle)
```

 Haskell は遅延実行を行うため、`putStrLn txts` をしようとするとき、`txts` にはファイルの内容が入っているのではなく、`txts` には `withFile "main.hs" ReadMode hGetContents` を実行して中身を取り出す、という実行すべき内容が記されているだけなのである。<br>
そのため、`putStrLn txts` を処理するときになって初めてファイルの内容の読み込みを始めようとする訳なのであるが、そのときには `withFile` 関数から抜けてしまっているのでファイルがクローズされていて読めない、という状況に陥る。

もし、`withFile` を抜けた後にファイル内容を処理したいとするならば、`withFile` の中で式を強制的に評価させてファイル内容の読み込みをさせるしかない。

今の段階で、以下のプログラムの内容を理解する必要はないが、一応解決策を掲載しておく。<br>
以下のプログラムの内容を理解したい場合、`WHNF（弱冠頭正規形）` と `NF（正規形）` について調べると良いと思う。

```Haskell
import System.IO
import Control.DeepSeq

main = do
   txts <- withFile "main.hs" ReadMode (\hfile -> do  
         val <- hGetContents hfile
         return $!! val)  -- val `deepseq` return val と同じ意味

   putStrLn txts  -- withFile を抜けた後に、ファイル内容を表示している
```

## ファイルからの入力 `readFile`
`openFile` や `withFile` を使う場合は、ファイルのクローズ処理を念頭に置かなくてはならない。たくさんのファイルを開いたり閉じたりする場合は、ファイルのクローズ処理を適時行うことは重要である。

しかし、１つのプログラムで開くファイルはそれほど多くない、というのが多くの場合であると思う。そのような場合は、プログラムの終了時に一括して自動的にファイルを閉じさせれば良い、と考えれば簡単である。<br>
そのようにすれば、プログラムの終了時まで、自由にファイルの操作ができるようになる。

そのための関数が、`readFile :: FilePath -> IO String` である。使用例は以下の通り。
```Haskell
import System.IO

main = do
   txts <- readFile "main.hs"
   putStrLn txts
```

## ファイルへの出力
ファイルからの入力と同様に、ファイルハンドルを通して書き込む関数と、ファイルハンドルはシステムに任せて書き込みのみを指示する関数がある。

ファイルハンドルを通して書き込む場合は、`hPrint :: Show a => Handle -> a -> IO ()` を利用する。

ファイルハンドルをシステムに任せる場合は、`writeFile :: FilePath -> String -> IO ()` または `appendFile :: FilePath -> String -> IO ()` を利用する。<br>
`writeFile` を利用すると、書き込む前にファイル内容が空にされた後に書き込まれる、という動作になる。

簡単な例。
```Haskell
import System.IO     
   
main = do     
   txts <- readFile "main.hs"     
   writeFile "main2.hs" txts
```

## コマンドライン引数の取得

* `getArgs :: IO [String]`
コマンドラインの引数を保持している。

* `getProgName :: IO String`
プログラム名を保持している。

簡単な例。
```Haskell
import System.Environment   
import Data.List  

main = do  
   args <- getArgs  
   progName <- getProgName  
   putStrLn "The arguments are:"  
   mapM putStrLn args  
   putStrLn "The program name is:"  
   putStrLn progName  
```
上記プログラム中の `mapM` のシグネチャは、今の知識で見ても分かりにくいと思うので、今は以下のように考えておけば良い。

`mapM` は、`map` と同じようなものである。`args` は引数のリストとなるため、その要素を１つずつ取り出し、`putStrLn` に適用する、という動作になる。

## 例外処理






