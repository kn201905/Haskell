```Haskell
import Control.Monad (replicateM_)
import Control.Concurrent (threadDelay)
import Data.List (intercalate)
import System.Console.ANSI ( clearScreen, setCursorPosition )

w = 5 :: Int
h = 5 :: Int

--------------------------------------------
class Functor w => Comonad w where
  extract :: w a -> a
  extend :: (w b -> a) -> w b -> w a  -- bind に対応
  duplicate :: w a -> w (w a)  -- join に対応

  duplicate = extend id
  extend f = fmap f . duplicate

--------------------------------------------
data Z a = Z [a] a [a]

left, right :: Z a -> Z a
left (Z (l:ls) c rs) = Z ls l (c:rs)
right (Z ls c (r:rs)) = Z (c:ls) r rs

itrToList :: (a -> a) -> a -> [a]
itrToList f = tail . iterate f

instance Functor Z where
  fmap f (Z ls c rs) = Z (map f ls) (f c) (map f rs)

instance Comonad Z where
  extract (Z _ a _) = a
  duplicate z = Z (itrToList left z) z (itrToList right z)

toZ :: a -> [a] -> Z a
toZ a xs = Z (repeat a) a (xs ++ repeat a)

--------------------------------------------
newtype Z2 a = Z2 (Z (Z a))

instance Functor Z2 where
  fmap f (Z2 zz) = Z2 (fmap (fmap f) zz)

instance Comonad Z2 where
  extract (Z2 zz) = extract (extract zz)
  duplicate (Z2 zz) = fmap Z2 . Z2 $ roll . roll $ zz where
    roll zz = Z (itrToList (fmap left) zz) zz (itrToList (fmap right) zz)

toZ2 :: a -> [[a]] -> Z2 a
toZ2 a xss = Z2 $ toZ (toZ a []) (map (toZ a) xss)

--------------------------------------------
initField' :: [[Char]]
initField' =
  [ " # "
  , "  #"
  , "###"
  ]

initField :: [[Int]]
initField = map (map c2i) initField'
  where c2i c = if c == '#' then 1 else 0

showZ2 :: Z2 Int -> IO ()
showZ2 (Z2 (Z _ _ rows)) = do
  flip mapM_ (take 5 rows) $ \(Z _ _ row) -> putStrLn . map i2c $ take 5 row
  where i2c x = if x == 1 then '#' else ' '


cntNeighbors :: Z2 Int -> Int
cntNeighbors (Z2 (Z
  (Z (n0:_) n1 (n2: _):_)
  (Z (n3:_) _  (n4:_))
  (Z (n5:_) n6 (n7: _):_))) = n0 + n1 + n2 + n3 + n4 + n5 + n6 + n7

-- 評価値は 0 or 1
life :: Z2 Int -> Int
life zz =
  if extract zz /= 0
    then b2i (n == 2 || n == 3)
    else b2i (n == 3)
  where
    b2i :: Bool -> Int
    b2i b = if b then 1 else 0
    n = cntNeighbors zz
    
main :: IO ()
main = do
  loop (toZ2 0 initField) where
    loop zz = do
      clearScreen
      setCursorPosition 0 0
      showZ2 zz
      threadDelay 300000
      loop (extend life zz)
```
